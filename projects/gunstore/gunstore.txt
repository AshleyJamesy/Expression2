@name Advanced Gunstore
@inputs [MIRROR]:entity
@inputs [USER01, USER02, USER03, USER04, USER05, USER06, USER07, USER08, USER09, USER10, USER11, USER12, USER13, USER14, USER15, USER16, USER17, USER18, USER19, USER20]:wirelink
@persist [CATEGORIES, ITEMS, CART, SHIPMENTS]:table [TAX]:number
@persist [TAB]:string
@persist [PLAYER]:entity

#include "library/string"
#include "library/table"
#include "library/egp"
#include "library/user-ranger"

#include "projects/gunstore/items"

if(first() | dupefinished()) {
    CATEGORIES = sStoreCategories()
    ITEMS = sStoreItems()
    
    CART = table()
    SHIPMENTS = table()
    TAB = ""
    TAX = 0
    PLAYER = noentity()
    
    USER01:entity(), USER02:entity(), USER03:entity(), USER04:entity(), USER05:entity()
    USER06:entity(), USER07:entity(), USER08:entity(), USER09:entity(), USER10:entity()
    USER11:entity(), USER12:entity(), USER13:entity(), USER14:entity(), USER15:entity()
    USER16:entity(), USER17:entity(), USER18:entity(), USER19:entity(), USER20:entity()
}

function number table:isAvaliable() {
    return SHIPMENTS:exists(This["name", string]:lower())
}

function number table:isMaxInCart() {
    local CartItem = CART:findTable(function(Other:table, _:number) {
        return Other["item", table] == This
    })
    
    if(CartItem == notable()) {
        return 0
    }
    
    return CartItem["amount", number] >= (This["max", number] > 0 ? This["max", number] : 10)
}

function table:itemList(List:table) {
    local Style = table(
        "background" = "#191919",
        "button" = table(
            "active" = "#570df8",
            "disabled" = "#2d2d2d"
        ),
        "rounded" = 1
    )
    
    This:egpScrollableList(List, 9, 8, Style, 1, function(Event:table, _:function) {
        local ElementTarget = Event["target", table]
        
        local ListItem = Event["item", table]
        local Position = Event["position", table]
        local Size = Event["size", table]
        
        local Name = ListItem["name", string]
        local Price = ListItem["price", number]
        local Available = ListItem:isAvaliable()
        local MaxInCart = ListItem:isMaxInCart()
        
        local LabelColor = !Available ? "#646464" : "#ffffff"
        local LabelText = Name:length() > 17 ? Name:left(17):trim() + "..." : Name
        
        local PriceColor = !Available ? "#646464" : "#32c832"
        local PriceText = "$" + Price:comma()
        
        local ButtonColor = !Available ? "#c83232" : MaxInCart ? "#ffaa00" : "#32c832"
        local ButtonLabelColor = !Available ? "#8a1e1e" : MaxInCart ? "#ffffff" : "#c2ffcf"
        local ButtonLabelText = !Available ? "Out of stock" : MaxInCart ? "Max In Cart" : "Add to Cart"
        
        if(ElementTarget != egpNoElement()) {
            local ElementLabel = ElementTarget:egpChild("label")
            local ElementPrice = ElementTarget:egpChild("price")
            local ElementAdd = ElementTarget:egpChild("add")
            local ElementAddLabel = ElementAdd:egpChild("label")
            ElementLabel:egpColor(LabelColor)
            ElementLabel:egpText(LabelText)
            ElementPrice:egpColor(PriceColor)
            ElementPrice:egpText(PriceText)
            ElementAdd:egpColor(ButtonColor)
            ElementAddLabel:egpColor(ButtonLabelColor)
            ElementAddLabel:egpText(ButtonLabelText)
            return egpNoElement()
        }
        
        return table(
            "name" = "list-item",
            "anchor" = table("x" = "0%", "y" = "0%"),
            "position" = Position,
            "size" = Size,
            "color" = "#323232",
            "rounded" = 1,
            "children" = table(
                table(
                    "name" = "label",
                    "type" = "text",
                    "anchor" = table("x" = "0%", "y" = "0%"),
                    "position" = table("x" = "3%", "y" = 0),
                    "size" = table("width" = "47%", "height" = "100%"),
                    "color" = LabelColor,
                    "text" = LabelText,
                    "font" = table(
                        "font" = "",
                        "size" = "30%"
                    ),
                    "align" = vec2(0, 1),
                    "mouse-events" = "none"
                ),
                table(
                    "name" = "price",
                    "type" = "text",
                    "anchor" = table("x" = "0%", "y" = "0%"),
                    "position" = table( "x" = "48%", "y" = "0"),
                    "size" = table("width" = "20%", "height" = "100%"),
                    "color" = PriceColor,
                    "text" = PriceText,
                    "font" = table(
                        "font" = "",
                        "size" = "30%"
                    ),
                    "align" = vec2(0,1),
                    "mouse-events" = "none"
                ),
                table(
                    "name" = "add",
                    "anchor" = table("x" = "100%", "y" = "50%"),
                    "position" = table("x" = "98.5%", "y" = "50%"),
                    "size" = table("width" = "30%", "height" = "80%"),
                    "color" = ButtonColor,
                    "rounded" = 1,
                    "children" = table(
                        table(
                            "name" = "label",
                            "type" = "text",
                            "anchor" = table("x" = "0%", "y" = "0%"),
                            "position" = table("x" = "0%", "y" = "0%"),
                            "size" = table("width" = "100%", "height" = "100%"),
                            "color" = ButtonLabelColor,
                            "text" = ButtonLabelText,
                            "font" = table(
                                "font" = "",
                                "size" = "30%"
                            ),
                            "align" = vec2(1, 1),
                            "mouse-events" = "none"
                        )
                    ),
                    "events" = table(
                        "mousedown" = function(Event:table, _:function) {
                            if(PLAYER:isValid()) {
                                return
                            }
                            
                            local ElementRow = Event["target", table]:egpParent()
                            local Item = ElementRow["item", table]
                            
                            local Available = Item:isAvaliable()
                            if(!Available) {
                                return
                            }
                            
                            local MaxInCart = Item:isMaxInCart()
                            if(MaxInCart) {
                                return
                            }
                            
                            local CartItem = CART:findTable(function(Other:table, _:number) {
                                return Other["item", table] == Item
                            })
                            
                            if(CartItem == notable()) {
                                CartItem = table("item" = Item, "amount" = 0), CART:pushTable(CartItem)
                            }
                            CartItem["amount", number] = CartItem["amount", number] + 1
                            
                            #rerender the element
                            ElementRow["render", function]()
                            
                            egpScreen():soundPlay("buttons/lightswitch2.wav", 0.1, "buttons/lightswitch2.wav")
                            soundVolume("buttons/lightswitch2.wav", 0.5)
                            
                            local ElementCart = egpGet("document.main.tabs.cart")
                            ElementCart:egpEvent("added", table("target" = ElementCart))
                        }
                    )
                )
            )
        )
    })
}

function table:cartList(List:table, Hide:number) {
    local Style = table(
        "background" = "#191919",
        "button" = table(
            "active" = "#570df8",
            "disabled" = "#2d2d2d"
        ),
        "rounded" = 1
    )
    
    This:egpScrollableList(List, 6, 8, Style, Hide, function(Event:table, _:function) {
        local ElementTarget = Event["target", table]
        
        local ListItem = Event["item", table]
        local Position = Event["position", table]
        local Size = Event["size", table]
        
        local Amount = ListItem["amount", number]
        
        local Item = ListItem["item", table]
        local Name = Item["name", string]
        local Price = Item["price", number]
        
        local LabelText = Name:length() > 14 ? Name:left(14):trim() + "..." : Name
        local TotalText = "$" + (Price * Amount):comma()
        local AmountText = "x" + Amount:toString()
        
        if(ElementTarget != egpNoElement()) {
            local ElementLabel = ElementTarget:egpChild("label")
            local ElementTotal = ElementTarget:egpChild("total")
            local ElementAmount = ElementTarget:egpChild("amount")
            ElementLabel:egpText(LabelText)
            ElementTotal:egpText(TotalText)
            ElementAmount:egpText(AmountText)
            return egpNoElement()
        }
        
        return table(
            "name" = "list-item",
            "anchor" = table("x" = "0%", "y" = "0%"),
            "position" = Position,
            "size" = Size,
            "color" = "#323232",
            "rounded" = 1,
            "children" = table(
                table(
                    "name" = "label",
                    "type" = "text",
                    "position" = table("x" = "3%", "y" = 0),
                    "size" = table("width" = "47%", "height" = "100%"),
                    "color" = "#ffffff",
                    "text" = LabelText,
                    "font" = table(
                        "font" = "",
                        "size" = "35%"
                    ),
                    "align" = vec2(0, 1),
                    "mouse-events" = "none"
                ),
                table(
                    "name" = "total",
                    "type" = "text",
                    "position" = table("x" = "48%", "y" = 0),
                    "size" = table("width" = "22%", "height" = "100%"),
                    "color" = "#32c832",
                    "text" = TotalText,
                    "font" = table(
                        "font" = "",
                        "size" = "35%"
                    ),
                    "align" = vec2(0, 1),
                    "mouse-events" = "none"
                ),
                table(
                    "name" = "amount",
                    "type" = "text",
                    "position" = table("x" = "70%", "y" = 0),
                    "size" = table("width" = "10%", "height" = "100%"),
                    "color" = "#ffffff",
                    "text" = AmountText,
                    "font" = table(
                        "font" = "",
                        "size" = "35%"
                    ),
                    "align" = vec2(0, 1),
                    "mouse-events" = "none"
                ),
                table(
                    "name" = "minus",
                    "position" = table("x" = "81%", "y" = 4),
                    "size" = table("width" = 25, "height" = 25),
                    "color" = "#ff3232",
                    "rounded" = 1,
                    "children" = table(
                        table(
                            "type" = "text",
                            "text" = "-",
                            "align" = vec2(1, 1),
                            "color" = "#8a1e1e",
                            "font" = table(
                                "font" = "",
                                "size" = "80%"
                            ),
                            "mouse-events" = "none"
                        )
                    ),
                    "events" = table(
                        "mousedown" = function(Event:table, _:function) {
                            if(PLAYER:isValid()) {
                                return
                            }
                            
                            local ElementRow = Event["target", table]:egpParent()
                            local ListItem = ElementRow["item", table]
                            local Amount = ListItem["amount", number]
                            if(Amount > 1) {
                                ListItem["amount", number] = Amount - 1
                                ElementRow["render", function]()
                            } else {
                                for(I = CART:count(), 1, -1) {
                                    if(CART[I, table] == ListItem) {
                                        CART:remove(I)
                                        break
                                    }
                                }
                                
                                ElementRow:egpParent():egpParent():cartList(CART, 0)
                            }
                            
                            local ElementCart = egpGet("document.main.tabs.cart")
                            ElementCart:egpEvent("added", table("target" = ElementCart))
                            local ElementTotals = egpGet("document.main.content.checkout.payment.totals")
                            ElementTotals:egpEvent("added", table("target" = ElementTotals))
                        }
                    )
                ),
                table(
                    "name" = "remove",
                    "color" = "#ff3232",
                    "position" = table("x" = "90%", "y" = 4),
                    "size" = table("width" = 25, "height" = 25),
                    "rounded" = 1,
                    "children" = table(
                        table(
                            "type" = "text",
                            "text" = "X",
                            "align" = vec2(1,1),
                            "color" = "#8a1e1e",
                            "mouse-events" = "none"
                        )
                    ),
                    "events" = table(
                        "mousedown" = function(Event:table, _:function) {
                            if(PLAYER:isValid()) {
                                return
                            }
                            
                            local ElementRow = Event["target", table]:egpParent()
                            local ListItem = ElementRow["item", table]
                            for(I = CART:count(), 1, -1) {
                                if(CART[I, table] == ListItem) {
                                    CART:remove(I)
                                    break
                                }
                            }
                            ElementRow:egpParent():egpParent():cartList(CART, 0)
                            
                            local ElementCart = egpGet("document.main.tabs.cart")
                            ElementCart:egpEvent("added", table("target" = ElementCart))
                            local ElementTotals = egpGet("document.main.content.checkout.payment.totals")
                            ElementTotals:egpEvent("added", table("target" = ElementTotals))
                        }
                    )
                )
            )
        )
    })
}

function table:tabList(List:table) {
    This:egpList(List, 1, 14, 0, function(Event:table, _:function) {
        local ElementTarget = Event["target", table]
        local ListItem = Event["item", table]
        local Index = Event["index", number]
        local Position = Event["position", table]
        local Size = Event["size", table]
        
        local Name = ListItem["name", string]
        local Icon = ListItem["icon", string]
        
        local IsCart = Name == "cart"
        
        local ButtonColor = IsCart ? "#d99400" : (Index % 2 == 0 ? "#2d2d2d" : "#373737")
        
        local LabelColor = IsCart ? "#ffffff" : "#787878"
        local LabelText = IsCart ? "CART (0)" : Name:upper() + "  "
        local LabelAlign = IsCart ? vec2(1, 1) : vec2(2, 1)
        
        local IconText = Icon
        
        if(ElementTarget != egpNoElement()) {
            local ElementLabel = ElementTarget:egpChild("label")
            local ElementIcon = ElementTarget:egpChild("icon")
            ElementTarget:egpColor(ButtonColor)
            ElementLabel:egpColor(LabelColor)
            ElementLabel:egpText(LabelText)
            ElementLabel:egpAlign(LabelAlign)
            ElementIcon:egpColor(LabelColor)
            ElementIcon:egpText(IconText)
            return egpNoElement()
        }
        
        return table(
            "name" = Name,
            "anchor" = table("x" = "0%", "y" = "0%"),
            "position" = Position,
            "size" = Size,
            "color" = ButtonColor,
            "children" = table(
                table(
                    "name" = "label",
                    "type" = "text",
                    "anchor" = table("x" = "0%", "y" = "0%"),
                    "position" = table("x" = "0%", "y" = "0%"),
                    "size" = table("width" = "100%", "height" = "100%"),
                    "color" = LabelColor,
                    "text" = LabelText,
                    "align" = LabelAlign,
                    "mouse-events" = "none"
                ),
                table(
                    "name" = "icon",
                    "type" = "text",
                    "anchor" = table("x" = "0%", "y" = "0%"),
                    "position" = table("x" = "5%", "y" = 5),
                    "size" = table("width" = 64, "height" = 64),
                    "color" = LabelColor,
                    "text" = IconText,
                    "font" = table(
                        "font" = "csd",
                        "size" = 42
                    ),
                    "mouse-events" = "none"
                )
            ),
            "events" = table(
                "added" = function(Event:table, _:function) {
                    local ElementTarget = Event["target", table]
                    local Selected = ElementTarget:egpName() == TAB
                    
                    local ElementLabel = ElementTarget:egpChild("label")
                    if(ElementTarget:egpName() == "cart") {
                        ElementTarget:egpColor(Selected ? "#1e1e1e" : "#d99400")
                        ElementLabel:egpColor("#ffffff")
                        
                        local Count = 0
                        foreach(_:number, CartItem:table = CART) {
                            Count = Count + CartItem["amount", number]
                        }
                        ElementLabel:egpText("CART (" + Count + ")")
                    } else {
                        local ElementIcon = ElementTarget:egpChild("icon")
                        ElementTarget:egpColor(Selected ? "#1e1e1e" : (ElementTarget["index", number] % 2 == 0 ? "#2d2d2d" : "#373737"))
                        ElementLabel:egpColor(Selected ? "#ffffff" :"#787878")
                        ElementIcon:egpColor(Selected ? "#ffffff" : "#787878")
                    }
                },
                "mouseenter" = function(Event:table, _:function) {
                    local ElementTarget = Event["target", table]
                    if(TAB != ElementTarget:egpName()) {
                        local ElementLabel = ElementTarget:egpChild("label")
                        ElementLabel:egpColor("#ffffff")
                    }
                },
                "mouseexit" = function(Event:table, _:function) {
                    local ElementTarget = Event["target", table]
                    if(TAB != ElementTarget:egpName() & ElementTarget:egpName() != "cart") {
                        local ElementLabel = ElementTarget:egpChild("label")
                        ElementLabel:egpColor("#787878")
                    }
                },
                "mousedown" = function(Event:table, _:function) {
                    if(PLAYER:isValid()) {
                        return
                    }
                    
                    local ElementTarget = Event["target", table]
                    if(TAB == ElementTarget:egpName()) {
                        return
                    }
                    TAB = ElementTarget:egpName()
                    
                    egpScreen():soundPlay("buttons/lightswitch2.wav", 0.1, "buttons/lightswitch2.wav")
                    soundVolume("buttons/lightswitch2.wav", 0.5)
                    
                    foreach(_:number, ElementChild:table = ElementTarget:egpParent():egpChildren()) {
                        ElementChild:egpEvent("added", table("target" = ElementChild))
                    }
                    
                    local ElementContent = egpGet("document.main.content")
                    if(TAB != "cart") {
                        if(ElementContent:egpHasChild("items")) {
                            ElementContent:egpChild("items"):itemList(ITEMS[TAB, table])
                        } else {
                            ElementContent:egpRemoveChildren()
                            ElementContent:egpAdd(table(
                                "name" = "items",
                                "anchor" = table("x" = "50%", "y" = "50%"),
                                "position" = table("x" = "50%", "y" = "50%"),
                                "size" = table("width" = "95%", "height" = "95%"),
                                "events" = table(
                                    "added" = function(Event:table, _:function) {
                                        Event["target", table]:itemList(ITEMS[TAB, table])
                                    }
                                )
                            ))
                        }
                    } else {
                        if(ElementContent:egpHasChild("checkout")) {
                            local ElementItemList = ElementContent:egpChild("checkout"):egpChild("items")
                            ElementItemList:egpEvent("added", table("target" = ElementItemList))
                        } else {
                            ElementContent:egpRemoveChildren()
                            ElementContent:egpAdd(table(
                                "name" = "checkout",
                                "anchor" = table("x" = "50%", "y" = "50%"),
                                "position" = table("x" = "50%", "y" = "50%"),
                                "size" = table("width" = "95%", "height" = "95%"),
                                "children" = table(
                                    table(
                                        "name" = "items",
                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                        "position" = table("x" = "0%", "y" = "0%"),
                                        "size" = table("width" = "100%", "height" = "60%"),
                                        "events" = table(
                                            "added" = function(Event:table, _:function) {
                                                Event["target", table]:cartList(CART, 1)
                                            }
                                        )
                                    ),
                                    table(
                                        "name" = "payment",
                                        "anchor" = table("x" = "0%", "y" = "100%"),
                                        "position" = table("x" = "0%", "y" = "100%"),
                                        "size" = table("width" = "100%", "height" = "38%"),
                                        "children" = table(
                                            table(
                                                "anchor" = table("x" = "50%", "y" = "0%"),
                                                "position" = table("x" = "50%", "y" = "0%"),
                                                "size" = table("width" = "95%", "height" = 2),
                                                "color" = "#646464",
                                                "mouse-events" = "none"
                                            ),
                                            table(
                                                "name" = "totals",
                                                "anchor" = table("x" = "0%", "y" = "50%"),
                                                "position" = table("x" = "3%", "y" = "30%"),
                                                "size" = table("width" = "94%", "height" = "45%"),
                                                "mouse-events" = "none",
                                                "children" = table(
                                                    table(
                                                        "type" = "text",
                                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                                        "position" = table("x" = "0%", "y" = "0%"),
                                                        "size" = table("width" = "22%", "height" = "33.33%"),
                                                        "color" = "#a1a1a1",
                                                        "text" = "Subtotal:",
                                                        "align" = vec2(2,1),
                                                        "mouse-events" = "none"
                                                    ),
                                                    table(
                                                        "name" = "subtotal",
                                                        "type" = "text",
                                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                                        "position" = table("x" = "25%", "y" = "0%"),
                                                        "size" = table("width" = "25%", "height" = "33.33%"),
                                                        "color" = "#ffffff",
                                                        "text" = "$0",
                                                        "align" = vec2(2,1),
                                                        "mouse-events" = "none"
                                                    ),
                                                    table(
                                                        "type" = "text",
                                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                                        "position" = table("x" = "0%", "y" = "33.33%"),
                                                        "size" = table("width" = "22%", "height" = "33.33%"),
                                                        "color" = "#a1a1a1",
                                                        "text" = "Tax:",
                                                        "align" = vec2(2,1),
                                                        "mouse-events" = "none"
                                                    ),
                                                    table(
                                                        "name" = "tax",
                                                        "type" = "text",
                                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                                        "position" = table("x" = "25%", "y" = "33.33%"),
                                                        "size" = table("width" = "25%", "height" = "33.33%"),
                                                        "color" = "#ffffff",
                                                        "text" = "$0",
                                                        "align" = vec2(2,1),
                                                        "mouse-events" = "none"
                                                    ),
                                                    table(
                                                        "type" = "text",
                                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                                        "position" = table("x" = "0%", "y" = "66.66%"),
                                                        "size" = table("width" = "22%", "height" = "33.33%"),
                                                        "color" = "#a1a1a1",
                                                        "text" = "Total:",
                                                        "align" = vec2(2,1),
                                                        "mouse-events" = "none"
                                                    ),
                                                    table(
                                                        "name" = "total",
                                                        "type" = "text",
                                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                                        "position" = table("x" = "25%", "y" = "66.66%"),
                                                        "size" = table("width" = "25%", "height" = "33.33%"),
                                                        "color" = "#ffffff",
                                                        "text" = "$0",
                                                        "align" = vec2(2,1),
                                                        "mouse-events" = "none"
                                                    )
                                                ),
                                                "events" = table(
                                                    "added" = function(Event:table, _:function) {
                                                        local Target = Event["target", table]
                                                        
                                                        local SubTotal = 0
                                                        foreach(_:number, CartItem:table = CART) {
                                                            local Item = CartItem["item", table]
                                                            SubTotal += Item["price", number] * CartItem["amount", number]
                                                        }
                                                        
                                                        local TaxTotal = ceil(SubTotal * TAX)
                                                        local Total = SubTotal + TaxTotal
                                                        
                                                        Target:egpChild("subtotal"):egpText("$" + SubTotal:comma())
                                                        Target:egpChild("tax"):egpText("$" + TaxTotal:comma())
                                                        Target:egpChild("total"):egpText("$" + Total:comma())
                                                        
                                                        #ifdef tauTaxFactor()
                                                            timer("tax", 0)
                                                        #endif
                                                    }
                                                )
                                            ),
                                            table(
                                                "name" = "buttons",
                                                "anchor" = table("x" = "0%", "y" = "100%"),
                                                "position" = table("x" = "0%", "y" = "100%"),
                                                "size" = table("width" = "100%", "height" = "40%"),
                                                "children" = table(
                                                    table(
                                                        "name" = "purchase",
                                                        "anchor" = table("x" = "0%", "y" = "0%"),
                                                        "position" = table("x" = "0%", "y" = "0%"),
                                                        "size" = table("width" = "75%", "height" = "100%"),
                                                        "color" = "#32c832",
                                                        "rounded" = 1,
                                                        "children" = table(
                                                            table(
                                                                "name" = "label",
                                                                "type" = "text",
                                                                "color" = "#c2ffcf",
                                                                "text" = "Checkout",
                                                                "font" = table(
                                                                    "font" = "",
                                                                    "size" = 17
                                                                ),
                                                                "align" = vec2(1, 1),
                                                                "mouse-events" = "none"
                                                            )
                                                        ),
                                                        "events" = table(
                                                            "mousedown" = function(Event:table, _:function) {
                                                                if(PLAYER:isValid()) {
                                                                    return
                                                                }
                                                                
                                                                local User = Event["user", entity]
                                                                
                                                                local SubTotal = 0
                                                                foreach(_:number, CartItem:table = CART) {
                                                                    CartItem["tax", number] = TAX, SubTotal += CartItem["item", table]["price", number] * CartItem["amount", number]
                                                                }
                                                                
                                                                local TaxTotal = ceil(SubTotal * TAX)
                                                                local Total = SubTotal + TaxTotal
                                                                
                                                                if(User:money() < Total) {
                                                                    egpScreen():soundPlay("declined", 0.427, "buttons/weapon_cant_buy.wav")
                                                                    return
                                                                }
                                                                
                                                                local Timeout = 15
                                                                if(!moneyRequest(User, Total, "Advanced Gunstore", Timeout)) {
                                                                    egpScreen():soundPlay("declined", 0.427, "buttons/weapon_cant_buy.wav")
                                                                    return
                                                                }
                                                                
                                                                #ifdef tauTaxFactor()
                                                                    stoptimer("tax")
                                                                #endif
                                                                stoptimer("shipments")
                                                                
                                                                Event["target", table]:egpChild("label"):egpText("Processing...")
                                                                
                                                                PLAYER = User
                                                                timer("money.timeout", Timeout * 1000)
                                                            }
                                                        )
                                                    ),
                                                    table(
                                                        "name" = "clear",
                                                        "anchor" = table("x" = "100%", "y" = "0%"),
                                                        "position" = table("x" = "100%", "y" = "0%"),
                                                        "size" = table("width" = "22%", "height" = "100%"),
                                                        "color" = "#c83232",
                                                        "rounded" = 1,
                                                        "children" = table(
                                                            table(
                                                                "name" = "label",
                                                                "type" = "text",
                                                                "color" = "#8a1e1e",
                                                                "text" = "Clear",
                                                                "font" = table(
                                                                    "font" = "",
                                                                    "size" = 18
                                                                ),
                                                                "align" = vec2(1, 1),
                                                                "mouse-events" = "none"
                                                            )
                                                        ),
                                                        "events" = table(
                                                            "mousedown" = function(_:table, _:function) {
                                                                if(PLAYER:isValid()) {
                                                                    return
                                                                }
                                                                
                                                                CART:clear()
                                                                
                                                                egpScreen():soundPlay("cart-clear", 0.616, "buttons/combine_button7.wav")
                                                                
                                                                local ElementTotals = egpGet("document.main.content.checkout.payment.totals")
                                                                ElementTotals:egpEvent("added", table("target" = ElementTotals))
                                                                local ElementCart = egpGet("document.main.tabs.cart")
                                                                ElementCart:egpEvent("added", table("target" = ElementCart))
                                                                local ElementItemList = egpGet("document.main.content.checkout.items")
                                                                ElementItemList:egpEvent("added", table("target" = ElementItemList))
                                                            }
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            ))
                        }
                    }
                }
            )
        )
    })
}

if(egpUserChanged()) {
    egpSetUser(egpUser())
}

if(egpWirelinkChanged()) {
    egpMirrorEnable(MIRROR)
    
    egpDefaultFont("WireGPU_ConsoleFont")
    egpClear(512,512,function(Event:table, _:function) {
        local ElementDocument = Event["target", table]
        ElementDocument:egpAdd(table(
            "name" = "header",
            "anchor" = table("x" = "0%", "y" = "0%"),
            "position" = table("x" = "0%", "y" = "0%"),
            "size" = table("width" = "100%", "height" = "10%"),
            "color" = "#570df8",
            "children" = table(
                table(
                    "type" = "text",
                    "anchor" = table("x" = "0%", "y" = "0%"),
                    "position" = table("x" = "0%", "y" = "0%"),
                    "size" = table("width" = "100%", "height" = "50%"),
                    "color" = "#ffffff",
                    "text" = "ADVANCED GUNSTORE",
                    "font" = table(
                        "font" = "",
                        "size" = 18
                    ),
                    "align" = vec2(1, 2)
                ),
                table(
                    "type" = "text",
                    "anchor" = table("x" = "0%", "y" = "0%"),
                    "position" = table("x" = "0%", "y" = "50%"),
                    "size" = table("width" = "100%", "height" = "50%"),
                    "color" = "#ffffff",
                    "text" = "https://github.com/AshleyJamesy/Expression2",
                    "font" = table(
                        "font" = "",
                        "size" = 12
                    ),
                    "align" = vec2(1, 1)
                )
            )
        ))
        
        ElementDocument:egpAdd(table(
            "name" = "main",
            "anchor" = table("x" = "0%", "y" = "0%"),
            "position" = table("x" = "0%", "y" = "10%"),
            "size" = table("width" = "100%", "height" = "90%"),
            "color" = "#1e1e1e",
            "children" = table(
                table(
                    "name" = "tabs",
                    "anchor" = table("x" = "0%", "y" = "0%"),
                    "position" = table("x" = "0%", "y" = "0%"),
                    "size" = table("width" = "33%", "height" = "100%"),
                    "color" = "#2d2d2d",
                    "events" = table(
                        "added" = function(Event:table, _:function) {
                            local ElementTarget = Event["target", table]
                            ElementTarget:tabList(CATEGORIES)
                            
                            egpCallback(function(_:table, _:function) {
                                if(TAB == "" & ElementTarget:egpChildren():count() > 0) {
                                    TAB = ElementTarget:egpChild(1):egpName()
                                }
                                
                                local ElementTab = ElementTarget:egpChildren():findTable(function(Child:table, _:number) {
                                    return Child:egpName() == TAB
                                })
                                if(ElementTab != notable()) {
                                    TAB = ""
                                    egpCallback(function(Event:table, _:function){
                                        local ElementTarget = Event["target", table]
                                        ElementTarget:egpEvent("mousedown", Event)
                                    }, table("target" = ElementTab))
                                }
                            })
                        }
                    )
                ),
                table(
                    "name" = "content",
                    "anchor" = table("x" = "100%", "y" = "0%"),
                    "position" = table("x" = "100%", "y" = "0%"),
                    "size" = table("width" = "67%", "height" = "100%"),
                    "children" = table()
                )
            )
        ))
    })
    
    timer("shipments", 0)
}

if(clk("shipments")) {
    local AvailableShipments = table()
    for(I = 1, 20) {
        local Wirelink = ioGetInputWirelink("USER" + format("%02d", I))
        local Entity = Wirelink:userRanger(50)
        
        if(!Entity:isValid()) {
            continue
        }
        
        if(!Entity:isShipment()) {
            continue
        }
        
        local ShipmentName = Entity:shipmentName():lower()
        AvailableShipments[ShipmentName, number] = 1
        
        if(SHIPMENTS:exists(ShipmentName)) {
            SHIPMENTS[ShipmentName, table]["index", number] = I
            continue
        }
        
        SHIPMENTS[ShipmentName, table] = table(
            "index" = I,
            "name" = Entity:shipmentName(),
            "type" = Entity:shipmentType(),
            "class" = Entity:shipmentClass(),
            "amount" = Entity:shipmentAmount(),
            "total" = Entity:shipmentSize(),
            "price" = Entity:shipmentPrice()
        )
        
        egpCallback(function(Event:table, _:function) {
            if(!egpExists("document.main.content.items.container")) {
                return
            }
            
            foreach(_:number, ElementRow:table = egpGet("document.main.content.items.container"):egpChildren()) {
                if(ElementRow["item", table]["name", string]:lower() == Event["name", string]:lower()) {
                    ElementRow["render", function]()
                    break
                }
            }
        }, table("name" = ShipmentName))
    }
    
    foreach(ShipmentName:string, _:table = SHIPMENTS) {
        if(AvailableShipments:exists(ShipmentName)) {
            continue
        }
        
        SHIPMENTS:remove(ShipmentName)
        egpCallback(function(Event:table, _:function) {
            local Removed = 0
            for(I = CART:count(), 1, -1) {
                if(CART[I, table]["item", table]["name", string]:lower() == Event["name", string]:lower()) {
                    CART:remove(I), Removed = 1
                    break
                }
            }
            
            if(Removed) {
                local ElementCart = egpGet("document.main.tabs.cart")
                ElementCart:egpEvent("added", table("target" = ElementCart))
            }
            
            if(egpExists("document.main.content.items.container")) {
                foreach(_:number, ElementRow:table = egpGet("document.main.content.items.container"):egpChildren()) {
                    if(ElementRow["item", table]["name", string]:lower() == Event["name", string]:lower()) {
                        ElementRow["render", function]()
                        break
                    }
                }
            } else {
                if(Removed) {
                    local ElementTotals = egpGet("document.main.content.checkout.payment.totals")
                    ElementTotals:egpEvent("added", table("target" = ElementTotals))
                    local ElementItemList = egpGet("document.main.content.checkout.items")
                    ElementItemList:egpEvent("added", table("target" = ElementItemList))
                }
            }
        }, table("name" = ShipmentName))
    }
    
    timer("shipments", 1000)
}

if(moneyClk()) {
    stoptimer("money.timeout")
    
    egpCallback(function(_:table, _:function) {
        local Total = 0
        foreach(_:number, CartItem:table = CART) {
            Total = Total + CartItem["amount", number]
        }
        egpGet("document.main"):egpAdd(table(
            "name" = "dispense",
            "children" = table(
                table(
                    "name" = "dialog",
                    "anchor" = table("x" = "50%", "y" = "50%"),
                    "position" = table("x" = "50%", "y" = "50%"),
                    "size" = table("width" = "60%", "height" = "50%"),
                    "color" = "#262626",
                    "mouse-events" = "none",
                    "children" = table(
                        table(
                            "anchor" = table("x" = "0%", "y" = "0%"),
                            "position" = table("x" = "0%", "y" = "0%"),
                            "size" = table("width" = "100%", "height" = "15%"),
                            "color" = "#570df8",
                            "mouse-events" = "none",
                            "children" = table(
                                table(
                                    "type" = "text",
                                    "anchor" = table("x" = "50%", "y" = "50%"),
                                    "position" = table("x" = "50%", "y" = "50%"),
                                    "size" = table("width" = "100%", "height" = "50%"),
                                    "color" = "#ffffff",
                                    "text" = "Order Status",
                                    "font" = table(
                                        "font" = "",
                                        "size" = 18
                                    ),
                                    "align" = vec2(1, 1),
                                    "mouse-events" = "none"
                                )
                            )
                        ),
                        table(
                            "name" = "content",
                            "anchor" = table("x" = "0%", "y" = "0%"),
                            "position" = table("x" = "0%", "y" = "15%"),
                            "size" = table("width" = "100%", "height" = "85%"),
                            "mouse-events" = "none",
                            "children" = table(
                                table(
                                    "name" = "progress",
                                    "anchor" = table("x" = "50%", "y" = "50%"),
                                    "position" = table("x" = "50%", "y" = "25%"),
                                    "size" = table("width" = "75%", "height" = "20%"),
                                    "color" = "#1c1c1c",
                                    "rounded" = 1,
                                    "mouse-events" = "none",
                                    "children" = table(
                                        table(
                                            "name" = "container",
                                            "anchor" = table("x" = "0%", "y" = "50%"),
                                            "position" = table("x" = "1%", "y" = "50%"),
                                            "size" = table("width" = "97%", "height" = "95%"),
                                            "mouse-events" = "none",
                                            "children" = table(
                                                table(
                                                    "name" = "bar",
                                                    "size" = table("width" = "0%", "height" = "100%"),
                                                    "color" = "#570df8",
                                                    "mouse-events" = "none"
                                                ),
                                                table(
                                                    "name" = "text",
                                                    "type" = "text",
                                                    "anchor" = table("x" = "50%", "y" = "50%"),
                                                    "position" = table("x" = "50%", "y" = "50%"),
                                                    "size" = table("width" = "100%", "height" = "50%"),
                                                    "color" = "#ffffff",
                                                    "text" = "",
                                                    "font" = table(
                                                        "font" = "",
                                                        "size" = 18
                                                    ),
                                                    "align" = vec2(1, 1),
                                                    "mouse-events" = "none"
                                                )
                                            )
                                        ),
                                        table(
                                            "name" = "border",
                                            "color" = "#363636",
                                            "rounded" = 1,
                                            "outline" = 8,
                                            "mouse-events" = "none"
                                        )
                                    ),
                                    "events" = table(
                                        "added" = function(Event:table, _:function) {
                                            local Count = 0
                                            foreach(_:number, CartItem:table = CART) {
                                                Count = Count + CartItem["amount", number]
                                            }
                                            
                                            local Amount = (Total - Count)
                                            
                                            local Target = Event["target", table]
                                            local ElementContainer = Target:egpChild("container")
                                            local ElementBar = ElementContainer:egpChild("bar")
                                            local ElementText = ElementContainer:egpChild("text")
                                            ElementBar:egpSize(table(
                                                "width" = format("%0.2f%%", (Amount / Total) * 100),
                                                "height" = "100%"
                                            ))
                                            ElementText:egpText("Dispensing " + Amount + "/" + Total)
                                        }
                                    )
                                ),
                                table(
                                    "name" = "errors",
                                    "type" = "text",
                                    "anchor" = table("x" = "50%", "y" = "100%"),
                                    "position" = table("x" = "50%", "y" = "95%"),
                                    "size" = table("width" = "75%", "height" = "50%"),
                                    "color" = "#ffffff",
                                    "text" = "",
                                    "font" = table(
                                        "font" = "",
                                        "size" = 15
                                    ),
                                    "mouse-events" = "none"
                                )
                            )
                        )
                    )
                )
            )
        ))
        timer("dispense", 0)
    })
}

#ifdef tauTaxFactor()
if(clk("tax")) {
    local Levels = table(
        "1" = 0,
        "2" = 0.25,
        "3" = 0.375,
        "4" = 0.50,
        "5" = 0.625
    )
    
    local TaxLevel = Levels[tauTaxFactor(), number]
    if(TaxLevel != TAX) {
        TAX = TaxLevel
        print("Gunstore: Tax Automatically Updated: " + floor(TAX * 100) + "%")
        
        egpCallback(function(_:table, _:function) {
            if(!egpExists("document.main.content.checkout.payment.totals")) {
                return
            }
            
            if(PLAYER:isValid()) {
                return
            }
            
            local ElementCheckoutTotals = egpGet("document.main.content.checkout.payment.totals")
            ElementCheckoutTotals:egpEvent("added", table("target" = ElementCheckoutTotals))
        })
    }
    
    timer("tax", 1000)
}
#else
function handleChat(Player:entity, Message:string, _:number) {
    if(Player != owner()) {
        return
    }
    
    local Arguments = Message:replaceRE("%s+", " "):explode(" ")
    if(Arguments:removeString(1):lower() != "!gunstore") {
        return
    }
    
    hideChat(1)
    
    local Command = Arguments:removeString(1):lower()
    switch(Command) {
        case "tax",
            TAX = Arguments:removeString(1):toNumber()
            print("Gunstore: Tax Updated: " + floor(TAX * 100) + "%")
            
            egpCallback(function(_:table, _:function) {
                if(!egpExists("document.main.content.checkout.payment.totals")) {
                    return
                }
                
                if(PLAYER:isValid()) {
                    return
                }
                
                local ElementCheckoutTotals = egpGet("document.main.content.checkout.payment.totals")
                ElementCheckoutTotals:egpEvent("added", table("target" = ElementCheckoutTotals))
            })
        break
        default,
            hideChat(0)
        break
    }
}

event chat(Player:entity, Message:string, Team:number) {
    handleChat(Player, Message, Team)
}
#endif

if(clk("dispense")) {
    if(CART:count() > 0) {
        for(I = min(CART:count(), 20), 1, -1) {
            local CartItem = CART[I, table]
            local Amount = CartItem["amount", number]
            
            local Item = CartItem["item", table]
            local ItemName = Item["name", string]
            local Shipment = SHIPMENTS[ItemName:lower(), table]
            
            if(Amount > 0) {
                local Wirelink = ioGetInputWirelink("USER" + format("%02d", Shipment["index", number]))
                local Entity = Wirelink:userRanger(50)
                
                #TODO: check if shipment here is correct, could of changed
                if(Entity:isValid() && Entity:isShipment()) {
                    CartItem["amount", number] = Amount - 1
                    
                    Wirelink["Fire", number] = 1
                    timer("users", 0)
                    
                    if(CartItem["amount", number] == 0) {
                        CART:remove(I)
                    }
                    
                    egpCallback(function(Event:table, _:function) {
                        local ElementErrors = egpGet("document.main.dispense.dialog.content.errors")
                        local Text = ElementErrors["properties", table]["text", string]
                        Text = (Event["name", string]:length() > 14 ? Event["name", string]:left(14):trim() + "..." : Event["name", string]) + " x1 - Dispensed\n" + Text
                        ElementErrors:egpText(Text)
                    }, table("name" = ItemName, "amount" = CartItem["amount", number]))                
                } else {
                    local Total = CartItem["amount", number] * Item["price", number] * (1 + CartItem["tax", number])
                    moneyGive(PLAYER, Total)
                    CART:remove(I)
                    
                    egpCallback(function(Event:table, _:function) {
                        local ElementErrors = egpGet("document.main.dispense.dialog.content.errors")
                        local Text = ElementErrors["properties", table]["text", string]
                        Text = (Event["name", string]:length() > 14 ? Event["name", string]:left(14):trim() + "..." : Event["name", string]) + " x" + Event["amount", number] + " - Refunded\n" + Text
                        ElementErrors:egpText(Text)
                    }, table("name" = ItemName, "amount" = CartItem["amount", number]))
                }
            }
        }
        
        egpCallback(function(_:table, _:function) {
            local ElementProgress = egpGet("document.main.dispense.dialog.content.progress")
            ElementProgress:egpEvent("added", table("target" = ElementProgress))
        })
    }
    
    if(CART:count() == 0) {
        timer("dialog.remove", 2000)
    } else {
        timer("dispense", 2000)
    }
}

if(clk("users")) {
    for(I = 1, 20) {
        local Wirelink = ioGetInputWirelink("USER" + format("%02d", I))
        Wirelink["Fire", number] = 0
    }
}

if(clk("dialog.remove")) {
    egpCallback(function(_:table, _:function) {
        PLAYER = noentity()
        
        local ElementItemList = egpGet("document.main.content.checkout.items")
        ElementItemList:egpEvent("added", table("target" = ElementItemList))
        
        local ElementCheckoutTotals = egpGet("document.main.content.checkout.payment.totals")
        ElementCheckoutTotals:egpEvent("added", table("target" = ElementCheckoutTotals))
        
        local ElementCheckoutButton = egpGet("document.main.content.checkout.payment.buttons.purchase")
        ElementCheckoutButton:egpChild("label"):egpText("Checkout")
        
        local ElementCart = egpGet("document.main.tabs.cart")
        ElementCart:egpEvent("added", table("target" = ElementCart))
        
        egpGet("document.main.dispense"):egpRemove()
        
        #ifdef tauTaxFactor()
            timer("tax", 0)
        #endif
        timer("shipments", 0)
    })
}

if(moneyNoClk() | moneyTimeout() | clk("money.timeout")) {
    stoptimer("money.timeout")
    egpScreen():soundPlay("checkout-declined", 0.427, "buttons/weapon_cant_buy.wav")
    
    egpCallback(function(_:table, _:function) {
        PLAYER = noentity()
        
        local ElementCheckoutButton = egpGet("document.main.content.checkout.payment.buttons.purchase")
        ElementCheckoutButton:egpChild("label"):egpText("Checkout")
        
        #ifdef tauTaxFactor()
            timer("tax", 0)
        #endif
        timer("shipments", 0)
    })
}

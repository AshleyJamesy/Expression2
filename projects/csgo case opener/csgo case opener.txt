@name CSGO Case Opener
@persist TIME:number ITEMS:table DECK:table
@persist SPEED:number PRIZE:string COUNT:number
@persist [BET, MULTIPLIER]:number PLAYER:entity

#include "library/string"
#include "library/number"
#include "library/egp"

if(first() || dupefinished()) {
    BET = 10000
    MULTIPLIER = 1
    
    #-----------------------------------------------------------------------------
    
    PLAYER = noentity()
    TIME = 0
    ITEMS = table()
    DECK = table()
    SPEED = 0
    PRIZE = ""
    COUNT = 0
    
    function table:egpRefreshGun(S_NAME:string) {
        local Item = table(
            "name" = "",
            "color" = vec(150, 150, 150)
        )
        
        if(ITEMS:exists(S_NAME)) {
            Item = ITEMS[S_NAME, table]
        }
        
        local Size = vec2(140, 137)
        local Name = Item["name", string]
        local Colour = Item["color", vector]
        local Material = Item["material", string]
        local ImageSize = Item["size", vector2]
        
        local ElementLabel = This:egpChild("label")
        ElementLabel:egpColor(vec4(Colour, 255))
        local ElementText = ElementLabel:egpChild("text")
        ElementText:egpText(Name)
        
        local ElementImage = This:egpChild("image-container"):egpChild("image")
        local ElementImageId = ElementImage:egpId()
        ElementImage["material", string] = Material
        EGP_SCREEN:egpMaterial(ElementImageId, ElementImage["material", string])
        EGP_SCREEN:egpSize(ElementImageId, Size * vec2(1.0, 0.67) * ImageSize)
        EGP_SCREEN:egpPos(ElementImageId, Size * vec2(1.0, 0.67) * 0.5 - Size * vec2(1.0, 0.67) * ImageSize * 0.5)
        
        if(ElementImage["material", string] == "") {
            EGP_SCREEN:egpColor(ElementImageId, vec4(255, 255, 255, 0))
        } else {
            EGP_SCREEN:egpColor(ElementImageId, vec4(255, 255, 255, 255))
        }
        
        if(Name == "Knife") {
            EGP_SCREEN:egpPos(ElementImageId, Size * vec2(1.4, 0.4) * 0.5 - Size * vec2(1.0, 0.67) * ImageSize * 0.5)
            EGP_SCREEN:egpColor(ElementImageId, Colour * 0.5)
            EGP_SCREEN:egpAngle(ElementImageId, -25)
        } else {
            EGP_SCREEN:egpColor(ElementImageId, vec(255,255,255))
            EGP_SCREEN:egpAngle(ElementImageId, 0)
        }
    }
    
    function void cItem(S_NAME:string, V_COLOUR:vector, S_MATERIAL:string, V2_IMAGE_SIZE:vector2, S_RARITY:string, N_PROBABILITY:number, N_MULTIPLIER:number) {
        ITEMS[S_NAME, table] = table(
            "name" = S_NAME,
            "color" = V_COLOUR,
            "material" = S_MATERIAL,
            "size" = V2_IMAGE_SIZE,
            "rarity" = S_RARITY,
            "probability" = N_PROBABILITY,
            "multiplier" = N_MULTIPLIER
        )
    }
    
    function string cRandomItem() {
        local Total = 0
        foreach(_:string, Item:table = ITEMS) {
            Total = Total + Item["probability", number]
        }
        
        local Random = random() * Total
        local Name = ""
        
        foreach(_:string, Item:table = ITEMS) {
            if(Random < Item["probability", number]) {
                Name = Item["name", string]
                break
            } else {
                Random = Random - Item["probability", number]
            }
        }
        
        return Name
    }
    
    function table:cUnboxItem() {
        moneyGive(PLAYER, BET * This["multiplier", number] * MULTIPLIER)
        PLAYER = noentity()
        
        
        local Sound = "pcasino/basicslotmachine/win_tune.wav"
        
        if(This["rarity", string] == "uncommon") {
            Sound = "pcasino/basicslotmachine/fail_tune.wav"
        }
        
        EGP_SCREEN:entity():soundPlay(Sound, 2.09, Sound)
        soundVolume(Sound, 0.5)
    }
    
    #uncommon
    cItem("P228", vec(75,100,225), "vgui/gfx/vgui/p228", vec2(0.8, 0.6), "uncommon", 0.8, 0.0) #80.0%
    cItem("Glock", vec(75,100,225), "vgui/gfx/vgui/glock18", vec2(0.8, 0.6), "uncommon", 0.75, 0.3) #75.0%
    cItem("USP", vec(75,100,225), "vgui/gfx/vgui/usp45", vec2(0.8, 0.6), "uncommon", 0.70, 0.5) #70.0%
    cItem("Five Seven", vec(75,100,225), "vgui/gfx/vgui/fiveseven", vec2(0.8, 0.6), "uncommon", 0.65, 0.8) #65.0%
    cItem("Berettes", vec(75,100,225), "vgui/gfx/vgui/elites", vec2(0.8, 0.5), "uncommon", 0.6, 0.9) #60.0%

    #restricted
    cItem("Scout", vec(125,0,255), "vgui/gfx/vgui/scout", vec2(0.9, 0.4), "restricted", 0.4, 1.2) #15.0%
    cItem("XM1014", vec(125,0,255), "vgui/gfx/vgui/xm1014", vec2(0.9, 0.5), "restricted", 0.4, 1.3) #15.0%
    cItem("Mac10", vec(125,0,255), "vgui/gfx/vgui/mac10", vec2(0.8, 0.6), "restricted", 0.4, 1.4) #15.0%
    cItem("MP5", vec(125,0,255), "vgui/gfx/vgui/mp5", vec2(0.8, 0.8), "restricted", 0.4, 1.5) #15.0%

    #classified
    cItem("Famas", vec(225,0,255), "vgui/gfx/vgui/famas", vec2(0.9, 0.6), "classified", 0.03, 2.0) #3.0%
    cItem("P90", vec(225,0,225), "vgui/gfx/vgui/p90", vec2(0.8, 0.7), "classified", 0.03, 2.2) #3.0%
    cItem("AUG", vec(225,0,225), "vgui/gfx/vgui/aug", vec2(1, 0.7), "classified", 0.03, 2.5) #3.0%
    cItem("Deagle", vec(225,0,225), "vgui/gfx/vgui/deserteagle", vec2(0.9, 0.8), "classified", 0.03, 2.8) #3.0%

    #covert
    cItem("M4A1S", vec(225,0,50), "vgui/gfx/vgui/m4a1", vec2(0.8, 0.5), "covert", 0.0064, 4.0) #0.64%
    cItem("AK47", vec(225,0,50), "vgui/gfx/vgui/ak47", vec2(0.9, 0.5), "covert", 0.0064, 4.3) #0.64%
    cItem("AWP", vec(225,0,50), "vgui/gfx/vgui/awp", vec2(0.9, 0.5), "covert", 0.0064, 4.8) #0.64%

    #rare
    cItem("Knife", vec(255,175,0), "vgui/hud/m9k_knife", vec2(0.7, 0.7), "rare", 0.0025, 10.0) #0.25%
}

if(moneyClk()) {
    if(PLAYER == owner()) {
        PRIZE = "Knife"
    } else {
        PRIZE = cRandomItem()
    }
    
    SPEED = 1
    COUNT = 25
    egpScreen():soundPlay("pcasino/other/lever.wav", 0.311, "pcasino/other/lever.wav")
    soundVolume("pcasino/other/lever.wav", 0.5)
    
    timer("tick", 0)
}

if(moneyNoClk()) {
    PLAYER = noentity()
}

if(moneyTimeout()) {
    PLAYER = noentity()
}

event playerSpawn(Player:entity) {
    if(Player:deaths() == 0) {
        egpWirelink():egpFixMaterials()
    }
}

if(egpWirelinkChanged()) {
    egpClear(512,512,function(Event:table, _:function) {
        local Target = Event["target", table]
        Target:egpAdd(table(
            "name" = "background",
            "color" = "#05054b"
        ))
        
        Target:egpAdd(table(
            "name" = "header",
            "position" = table("x" = "2.5%", "y" = "2.5%"),
            "size" = table("width" = "95%", "height" = "10%"),
            "color" = "#050532",
            "children" = table(
                table(
                    "name" = "text",
                    "type" = "text",
                    "color" = "#FFFFFF",
                    "text" = "CSGO CASE OPENER",
                    "align" = vec2(1, 1),
                    "font" = table(
                        "font" = "",
                        "size" = 26
                    )
                )
            )
        ))
        
        Target:egpAdd(table(
            "name" = "content",
            "color" = "#050532",
            "position" = table("x" = 0, "y" = 75),
            "size" = table("width" = "100%", "height" = 158),
            "children" = table(
                table(
                    "name" = "deck",
                    "anchor" = table("x" = "0%", "y" = "50%"),
                    "position" = table("x" = "0%", "y" = "50%"),
                    "events" = table(
                        "added" = function(Event:table, _:function) {
                            local Target = Event["target", table]
                            for(I = -2, 3) {
                                Target:egpAdd(table(
                                    "name" = "card",
                                    "position" = table("x" = (256 - 140 * 0.5) + 145 * I, "y" = 0),
                                    "size" = table("width" = 140, "height" = 137),
                                    "color" = "#05052d",
                                    "children" = table(
                                        table(
                                            "name" = "effect01",
                                            "color" = "#969696",
                                            "size" = table("width" = "100%", "height" = "67%"),
                                            "material" = "gui/gradient_down"
                                        ),
                                        table(
                                            "name" = "image-container",
                                            "size" = table("width" = "100%", "height" = "67%"),
                                            "children" = table(
                                                table(
                                                    "name" = "image"
                                                )
                                            )
                                        ),
                                        table(
                                            "name" = "label",
                                            "position" = table("x" = 0, "y" = "67%"),
                                            "size" = table("width" = "100%", "height" = "33%"),
                                            "color" = "#969696",
                                            "material" = "gui/gradient_up",
                                            "children" = table(
                                                table(
                                                    "name" = "text",
                                                    "type" = "text",
                                                    "color" = "#FFFFFF",
                                                    "text" = "",
                                                    "align" = vec2(1, 1),
                                                    "font" = table(
                                                        "font" = "",
                                                        "size" = 20
                                                    )
                                                )
                                            )
                                        )
                                    ),
                                    "events" = table(
                                        "added" = function(Event:table,_:function) {
                                            local Item = cRandomItem()
                                            DECK:pushString(Item)
                                            Event["target", table]:egpRefreshGun(Item)
                                        }
                                    )
                                ))
                            }
                            
                            egpCallback(function(_:table,_:function) {
                                timer("tick", 0)
                            })
                        }
                    )
                )
            )
        ))
        
        Target:egpAdd(table(
            "name" = "info",
            "position" = table("x" = "2.5%", "y" = "48%"),
            "size" = table("width" = "95%", "height" = 190),
            "events" = table(
                "added" = function(Event:table, _:function) {
                    local Target = Event["target", table]
                    local ElementColumn1 = Target:egpChild(1)
                    local ElementColumn2 = Target:egpChild(2)
                    local ElementColumn3 = Target:egpChild(3)
                    
                    foreach(K:number, ItemName:string = array("P228", "Glock", "USP", "Five Seven", "Berettes", "Scout")) {
                        local Item = ITEMS[ItemName, table]
                        local Color = vec4(Item["color", vector], 255)
                        local Text = ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma()
                        ElementColumn1:egpAdd(table(
                            "type" = "text",
                            "position" = table("x" = 0, "y" = (K - 1) * 28),
                            "size" = table("width" = "100%", "height" = "15%"),
                            "color" = Color,
                            "text" = Text,
                            "align" = vec2(0, 1)
                        )
                    )}
                    
                    foreach(K:number, ItemName:string = array("XM1014", "Mac10", "MP5", "Famas", "P90", "AUG")) {
                        local Item = ITEMS[ItemName, table]
                        local Color = vec4(Item["color", vector], 255)
                        local Text = ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma()
                        ElementColumn2:egpAdd(table(
                            "type" = "text",
                            "position" = table("x" = 0, "y" = (K - 1) * 28),
                            "size" = table("width" = "100%", "height" = "15%"),
                            "color" = Color,
                            "text" = Text,
                            "align" = vec2(1, 1)
                        ))
                    }
                    
                    foreach(K:number, ItemName:string = array("Deagle", "M4A1S", "AK47", "AWP", "Knife")) {
                        local Item = ITEMS[ItemName, table]
                        local Color = vec4(Item["color", vector], 255)
                        local Text = ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma()
                        ElementColumn3:egpAdd(table(
                            "type" = "text",
                            "position" = table("x" = 0, "y" = (K - 1) * 28),
                            "size" = table("width" = "100%", "height" = "15%"),
                            "color" = Color,
                            "text" = Text,
                            "align" = vec2(1, 1)
                        ))
                    }
                }
            ),
            "children" = table(
                table(
                    "size" = table("width" = "33%", "height" = "100%")
                ),
                table(
                    "position" = table("x" = "33%", "y" = 0),
                    "size" = table("width" = "33%", "height" = "100%")
                ),
                table(
                    "position" = table("x" = "66%", "y" = 0),
                    "size" = table("width" = "33.8%", "height" = "100%")
                )
            )
        ))
        
        Target:egpAdd(table(
            "name" = "buy",
            "position" = table("x" = "2.5%", "y" = 450),
            "size" = table("width" = "95%", "height" = "10%"),
            "color" = "#009600",
            "events" = table(
                "mousedown" = function(Event:table, _:function) {
                    if(COUNT > 0 | PLAYER != noentity()) {
                        return
                    }
                    
                    local User = Event["user", entity]
                    if(moneyRequest(User, BET * MULTIPLIER, 15, "Open Case")) {
                        PLAYER = User
                    }
                }
            ),
            "children" = table(
                table(
                    "color" = "#001900",
                    "material" = "gui/gradient_up",
                    "mouse-events" = "none"
                ),
                table(
                    "name" = "text",
                    "type" = "text",
                    "color" = "#003200",
                    "text" = "Open Case ($" + (BET * MULTIPLIER):comma() + ")",
                    "align" = vec2(1, 1),
                    "font" = table(
                        "font" = "",
                        "size" = 22
                    ),
                    "mouse-events" = "none"
                )
            )
        ))
        
        Target:egpAdd(table(
            "name" = "slider",
            "position" = table("x" = "2.5%", "y" = 425),
            "size" = table("width" = "95%", "height" = 15),
            "color" = "#323264",
            "rounded" = 1,
            "events" = table(
                "mousedown" = function(Event:table, _:function) {
                    if(COUNT > 0 | PLAYER != noentity()) {
                        return
                    }
                    
                    local Target = Event["target", table]
                    local CursorPosition = Event["position", vector2]
                    
                    local ElementScrollButton = Target:egpChild("button")
                    local Position = vec2(clamp(CursorPosition:x() - 16, 8, Target:egpSize():x() - 8), 8)
                    ElementScrollButton:egpPos(Position)
                    
                    local Percent = (Position:x() / (Target:egpSize():x() - 8))
                    MULTIPLIER = roundNearest(1 + (Percent - 0.5), 0.05)
                    
                    local ElementBuyText = egpGet("document.buy"):egpChild("text")
                    ElementBuyText:egpText("Open Case ($" + (BET * MULTIPLIER):comma() + ")")
                    
                    local ElementInfo = egpGet("document.info")
                    local ElementColumn01 = ElementInfo:egpChild(1)
                    local ElementColumn02 = ElementInfo:egpChild(2)
                    local ElementColumn03 = ElementInfo:egpChild(3)
                    
                    foreach(K:number, ItemName:string = array("P228", "Glock", "USP", "Five Seven", "Berettes", "Scout")) {
                        local Item = ITEMS[ItemName, table]
                        ElementColumn01:egpChild(K):egpText(ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma())
                    }
                    
                    foreach(K:number, ItemName:string = array("XM1014", "Mac10", "MP5", "Famas", "P90", "AUG")) {
                        local Item = ITEMS[ItemName, table]
                        ElementColumn02:egpChild(K):egpText(ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma())
                    }
                    
                    foreach(K:number, ItemName:string = array("Deagle", "M4A1S", "AK47", "AWP", "Knife")) {
                        local Item = ITEMS[ItemName, table]
                        ElementColumn03:egpChild(K):egpText(ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma())
                    }
                }
            ),
            "children" = table(
                table(
                    "name" = "button",
                    "type" = "circle",               
                    "position" = table("x" = "50%", "y" = 8),
                    "size" = vec2(8,8),
                    "color" = "#050532",
                    "mouse-events" = "none"
                )
            )
        ))
    })
}

if(clk("tick")) {
    TIME += (SPEED * COUNT) * 1/60
    
    local ElementDeck = egpGet("document.content.deck")
    if(TIME >= 1.0) {
        TIME = 0
        
        if(COUNT > 0) {
            COUNT--
        }
        
        if(COUNT == 0) {
            SPEED = 0
            ITEMS[PRIZE, table]:cUnboxItem()
        }
        
        DECK:shift()
        if(COUNT == 3) {
            DECK:pushString(PRIZE)
        } else {
            DECK:pushString(cRandomItem())
        }
        
        foreach(K:number, ItemName:string = DECK) {
            ElementDeck:egpChild(K):egpRefreshGun(ItemName)
        }
        
        EGP_SCREEN:entity():soundPlay("buttons/lightswitch2.wav", 0.076, "buttons/lightswitch2.wav")
        soundVolume("buttons/lightswitch2.wav", 0.5)
        soundPitch("buttons/lightswitch2.wav", 100 - ((50 - COUNT) / 50) * 50)
    }
    
    EGP_SCREEN:egpPos(ElementDeck:egpId(), vec2(0, 10) + TIME * -vec2(145, 0))
    
    if(COUNT > 0) {
        timer("tick", 0)
    }
}

@name CSGO Case Opener
@persist TIME:number ITEMS:table DECK:table
@persist SPEED:number PRIZE:string COUNT:number
@persist [BET, MULTIPLIER]:number PLAYER:entity

#include "library/egp/1.0/init"

if(first() || dupefinished()) {
    BET = 10000
    MULTIPLIER = 1
    
    #-----------------------------------------------------------------------------
    
    PLAYER = noentity()
    TIME = 0
    ITEMS = table()
    DECK = table()
    SPEED = 0
    PRIZE = ""
    COUNT = 0
    
    function table:dRefreshGun(S_NAME:string) {
        local Item = table(
            "name" = "",
            "colour" = vec(150, 150, 150)
        )
        
        if(ITEMS:exists(S_NAME)) {
            Item = ITEMS[S_NAME, table]
        }
        
        local Size = vec2(140, 137)
        local Name = Item["name", string]
        local Colour = Item["colour", vector]
        local Material = Item["material", string]
        local ImageSize = Item["size", vector2]
        
        local ElementLabel = This:dGetChild("label")
        ElementLabel:dColour(vec4(Colour, 255))
        local ElementText = ElementLabel:dGetChild("text")
        ElementText:dText(Name)
        
        local ElementImage = This:dGetChild("image-container"):dGetChild("image")
        local ElementImageId = ElementImage:dId()
        ElementImage["material", string] = Material
        EGP_SCREEN:egpMaterial(ElementImageId, ElementImage["material", string])
        EGP_SCREEN:egpSize(ElementImageId, Size * vec2(1.0, 0.67) * ImageSize)
        EGP_SCREEN:egpPosChild(ElementImageId, Size * vec2(1.0, 0.67) * 0.5 - Size * vec2(1.0, 0.67) * ImageSize * 0.5)
        
        if(ElementImage["material", string] == "") {
            EGP_SCREEN:egpColor(ElementImageId, vec4(255, 255, 255, 0))
        } else {
            EGP_SCREEN:egpColor(ElementImageId, vec4(255, 255, 255, 255))
        }
        
        if(Name == "Knife") {
            EGP_SCREEN:egpPosChild(ElementImageId, Size * vec2(1.4, 0.4) * 0.5 - Size * vec2(1.0, 0.67) * ImageSize * 0.5)
            EGP_SCREEN:egpColor(ElementImageId, Colour * 0.5)
            EGP_SCREEN:egpAngle(ElementImageId, -25)
        } else {
            EGP_SCREEN:egpColor(ElementImageId, vec(255,255,255))
            EGP_SCREEN:egpAngle(ElementImageId, 0)
        }
    }
    
    function void cItem(S_NAME:string, V_COLOUR:vector, S_MATERIAL:string, V2_IMAGE_SIZE:vector2, S_RARITY:string, N_PROBABILITY:number, N_MULTIPLIER:number) {
        ITEMS[S_NAME, table] = table(
            "name" = S_NAME,
            "colour" = V_COLOUR,
            "material" = S_MATERIAL,
            "size" = V2_IMAGE_SIZE,
            "rarity" = S_RARITY,
            "probability" = N_PROBABILITY,
            "multiplier" = N_MULTIPLIER
        )
    }
    
    function string cRandomItem() {
        local Total = 0
        foreach(_:string, Item:table = ITEMS) {
            Total = Total + Item["probability", number]
        }
        
        local Random = random() * Total
        local Name = ""
        
        foreach(_:string, Item:table = ITEMS) {
            if(Random < Item["probability", number]) {
                Name = Item["name", string]
                break
            } else {
                Random = Random - Item["probability", number]
            }
        }
        
        return Name
    }
    
    function table:cUnboxItem() {
        moneyGive(PLAYER, BET * This["multiplier", number] * MULTIPLIER)
        PLAYER = noentity()
        
        
        local Sound = "pcasino/basicslotmachine/win_tune.wav"
        
        if(This["rarity", string] == "uncommon") {
            Sound = "pcasino/basicslotmachine/fail_tune.wav"
        }
        
        EGP_SCREEN:entity():soundPlay(Sound, 2.09, Sound)
        soundVolume(Sound, 0.5)
    }
    
    #uncommon
    cItem("P228", vec(75,100,225), "vgui/gfx/vgui/p228", vec2(0.8, 0.6), "uncommon", 0.6, 0.0) #80.0%
    cItem("Glock", vec(75,100,225), "vgui/gfx/vgui/glock18", vec2(0.8, 0.6), "uncommon", 0.6, 0.3) #80.0%
    cItem("USP", vec(75,100,225), "vgui/gfx/vgui/usp45", vec2(0.8, 0.6), "uncommon", 0.8, 0.5) #80.0%
    cItem("Five Seven", vec(75,100,225), "vgui/gfx/vgui/fiveseven", vec2(0.8, 0.6), "uncommon", 0.6, 0.8) #80.0%
    cItem("Berettes", vec(75,100,225), "vgui/gfx/vgui/elites", vec2(0.8, 0.5), "uncommon", 0.6, 0.9) #80.0%

    #restricted
    cItem("Scout", vec(125,0,255), "vgui/gfx/vgui/scout", vec2(0.9, 0.4), "restricted", 0.4, 1.2) #15.0%
    cItem("XM1014", vec(125,0,255), "vgui/gfx/vgui/xm1014", vec2(0.9, 0.5), "restricted", 0.4, 1.3) #15.0%
    cItem("Mac10", vec(125,0,255), "vgui/gfx/vgui/mac10", vec2(0.8, 0.6), "restricted", 0.4, 1.4) #15.0%
    cItem("MP5", vec(125,0,255), "vgui/gfx/vgui/mp5", vec2(0.8, 0.8), "restricted", 0.4, 1.5) #15.0%

    #classified
    cItem("Famas", vec(225,0,255), "vgui/gfx/vgui/famas", vec2(0.9, 0.6), "classified", 0.03, 2.0) #3.0%
    cItem("P90", vec(225,0,225), "vgui/gfx/vgui/p90", vec2(0.8, 0.7), "classified", 0.03, 2.2) #3.0%
    cItem("AUG", vec(225,0,225), "vgui/gfx/vgui/aug", vec2(1, 0.7), "classified", 0.03, 2.5) #3.0%
    cItem("Deagle", vec(225,0,225), "vgui/gfx/vgui/deserteagle", vec2(0.9, 0.8), "classified", 0.03, 2.8) #3.0%

    #covert
    cItem("M4A1S", vec(225,0,50), "vgui/gfx/vgui/m4a1", vec2(0.8, 0.5), "covert", 0.0064, 4.0) #0.64%
    cItem("AK47", vec(225,0,50), "vgui/gfx/vgui/ak47", vec2(0.9, 0.5), "covert", 0.0064, 4.3) #0.64%
    cItem("AWP", vec(225,0,50), "vgui/gfx/vgui/awp", vec2(0.9, 0.5), "covert", 0.0064, 4.8) #0.64%

    #rare
    cItem("Knife", vec(255,175,0), "vgui/hud/m9k_knife", vec2(0.7, 0.7), "rare", 0.0025, 10.0) #0.25%
}

event tick() {
    if(!dRendering()) {
        TIME += (SPEED * COUNT) * 0.01
        
        local ElementDeck = dBody():dGetChild("content"):dGetChild("deck")
        if(TIME >= 1.0) {
            TIME = 0
            
            if(COUNT > 0) {
                COUNT--
            }
            
            if(COUNT == 0) {
                SPEED = 0
                ITEMS[PRIZE, table]:cUnboxItem()
            }
            
            DECK:shift()
            if(COUNT == 3) {
                DECK:pushString(PRIZE)
            } else {
                DECK:pushString(cRandomItem())
            }
            
            foreach(K:number, ItemName:string = DECK) {
                ElementDeck:dGetChild(K):dRefreshGun(ItemName)
            }
            
            EGP_SCREEN:entity():soundPlay("buttons/lightswitch2.wav", 0.076, "buttons/lightswitch2.wav")
            soundVolume("buttons/lightswitch2.wav", 0.5)
            soundPitch("buttons/lightswitch2.wav", 100 - ((50 - COUNT)/50) * 50)
        }
        
        EGP_SCREEN:egpPosChild(ElementDeck:dId(), vec2(0, 10) + TIME * -vec2(145, 0))
    }
}

if(eEventClk("mouse.press")) {
    if(!dRendering()) {
        local Payload = eEventGetPayload()
    
        local Player = Payload["player", entity]
        local Cursor = Payload["cursor", vector2]
        
        local ElementBuy = dGetElement("body.buy")
        if(inrange(Cursor, ElementBuy:dPos(), ElementBuy:dPos() + ElementBuy:dSize())) {
            if(COUNT == 0 && PLAYER == noentity()) {
                if(moneyRequest(Player, BET * MULTIPLIER, 15, "Open Case")) {
                    PLAYER = Player
                }
            }
        }
        
        if(COUNT == 0 && PLAYER == noentity()) {
            local ElementScroll = dGetElement("body.scroll")
            if(inrange(Cursor, ElementScroll:dPos(), ElementScroll:dPos() + ElementScroll:dSize())) {
                local ElementScrollButton = ElementScroll:dGetChild("button")
                local Position = vec2(clamp(Cursor:x() - 16, 8, ElementScroll:dSize():x() - 8), 8)
                ElementScrollButton:dPos(Position)
                
                local Percent = (Position:x() / (ElementScroll:dSize():x() - 8))
                MULTIPLIER = roundNearest(1 + (Percent - 0.5), 0.05)
                
                local ElementBuyText = dGetElement("body.buy"):dGetChild("text")
                ElementBuyText:dText("Open Case ($" + (BET * MULTIPLIER):comma() + ")")
                
                local ElementInfo = dGetElement("body.info")
                local ElementColumn01 = ElementInfo:dGetChild(1)
                local ElementColumn02 = ElementInfo:dGetChild(2)
                local ElementColumn03 = ElementInfo:dGetChild(3)
                
                foreach(K:number, ItemName:string = array("P228", "Glock", "USP", "Five Seven", "Berettes", "Scout")) {
                    local Item = ITEMS[ItemName, table]
                    ElementColumn01:dGetChild(K):dText(ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma())
                }
                
                foreach(K:number, ItemName:string = array("XM1014", "Mac10", "MP5", "Famas", "P90", "AUG")) {
                    local Item = ITEMS[ItemName, table]
                    ElementColumn02:dGetChild(K):dText(ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma())
                }
                
                foreach(K:number, ItemName:string = array("Deagle", "M4A1S", "AK47", "AWP", "Knife")) {
                    local Item = ITEMS[ItemName, table]
                    ElementColumn03:dGetChild(K):dText(ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma())
                }
            }
        }
    }
}

if(moneyClk()) {
    if(PLAYER == owner()) {
        PRIZE = "Knife"
    } else {
        PRIZE = cRandomItem()
    }
    
    SPEED = 1
    COUNT = 45
    EGP_SCREEN:entity():soundPlay("pcasino/other/lever.wav", 0.311, "pcasino/other/lever.wav")
    soundVolume("pcasino/other/lever.wav", 0.5)
}

if(moneyNoClk()) {
    PLAYER = noentity()
}

if(moneyTimeout()) {
    PLAYER = noentity()
}

if(eEventClk("document.ready")) {
    local Body = eEventGetPayload()
    Body:dColour(vec4(5, 5, 75, 255))
    
    local CARDS = table(
        "name" = "deck",
        "type" = "box",
        "children" = table()
    )
    
    local COLUMN01 = table(
        "type" = "box",
        "size" = table(
            "width" = "33%",
            "height" = "100%"
        ),
        "children" = table()
    )
    
    local COLUMN02 = table(
        "type" = "box",
        "position" = table(
            "x" = "33%",
            "y" = 0
        ),
        "size" = table(
            "width" = "33%",
            "height" = "100%"
        ),
        "children" = table()
    )
    
    local COLUMN03 = table(
        "type" = "box",
        "position" = table(
            "x" = "66%",
            "y" = 0
        ),
        "size" = table(
            "width" = "33.8%",
            "height" = "100%"
        ),
        "children" = table()
    )
    
    Body:dChildren():pushTable(
        table(
            "name" = "header",
            "type" = "box",
            "colour" = vec4(5, 5, 50, 255),
            "position" = table(
                "x" = "2.5%",
                "y" = "2.5%"
            ),
            "size" = table(
                "width" = "95%",
                "height" = "10%"
            ),
            "children" = table(
                table(
                    "name" = "text",
                    "type" = "text",
                    "colour" = vec4(255, 255, 255, 255),
                    "text" = "CSGO CASE OPENER",
                    "align" = vec2(1, 1),
                    "font" = table(
                        "font" = "",
                        "size" = 26
                    )
                )
            )
        )
    )
    
    Body:dChildren():pushTable(
        table(
            "name" = "content",
            "type" = "box",
            "colour" = vec4(5, 5, 50, 255),
            "position" = table(
                "x" = 0,
                "y" = 75
            ),
            "size" = table(
                "width" = "100%",
                "height" = 158
            ),
            "children" = table(
                CARDS
            )
        )
    )
    
    Body:dChildren():pushTable(
        table(
            "name" = "pointer",
            "type" = "triangle",
            "colour" = vec4(25, 25, 75, 255)
        )
    )
    
    Body:dChildren():pushTable(
        table(
            "name" = "info",
            "type" = "box",
            "position" = table(
                "x" = "2.5%",
                "y" = "48%"
            ),
            "size" = table(
                "width" = "95%",
                "height" = 190
            ),
            "children" = table(
                COLUMN01,
                COLUMN02,
                COLUMN03
            )
        )
    )
    
    Body:dChildren():pushTable(
        table(
            "name" = "buy",
            "type" = "box",
            "colour" = vec4(0, 150, 0, 255),
            "size" = table(
                "width" = "95%",
                "height" = "10%"
            ),
            "position" = table(
                "x" = "2.5%",
                "y" = 450
            ),
            "children" = table(
                table(
                    "type" = "box",
                    "colour" = vec4(0, 25, 0, 255),
                    "material" = "gui/gradient_up"
                ),
                table(
                    "name" = "text",
                    "type" = "text",
                    "colour" = vec4(0, 50, 0, 255),
                    "text" = "Open Case ($" + (BET * MULTIPLIER):comma() + ")",
                    "align" = vec2(1, 1),
                    "font" = table(
                        "font" = "",
                        "size" = 22
                    )
                )
            )
        )
    )
    
    Body:dChildren():pushTable(
        table(
            "name" = "scroll",
            "type" = "box",
            "colour" = vec4(50, 50, 100, 255),
            "rounded" = 1,
            "size" = table(
                "width" = "95%",
                "height" = 15
            ),
            "position" = table(
                "x" = "2.5%",
                "y" = 425
            ),
            "children" = table(
                table(
                    "name" = "button",
                    "type" = "circle",
                    "colour" = vec4(5, 5, 50, 255),
                    "size" = vec2(8,8),
                    "position" = table(
                        "x" = "50%",
                        "y" = 8
                    )
                )
            )
        )
    )
    
    for(I = -2, 3) {
        DECK:pushString("")
        
        CARDS:dChildren():pushTable(
            table(
                "name" = "card",
                "type" = "box",
                "colour" = vec4(5, 5, 45, 255),
                "position" = table(
                    "x" = (256 - 140 * 0.5) + 145 * I,
                    "y" = 0  
                ),
                "size" = table(
                    "width" = 140,
                    "height" = 137
                ),
                "children" = table(
                    table(
                        "name" = "effect01",
                        "type" = "box",
                        "colour" = vec4(150, 150, 150, 255),
                        "size" = table(
                            "width" = "100%",
                            "height" = "67%"
                        ),
                        "material" = "gui/gradient_down"
                    ),
                    table(
                        "name" = "image-container",
                        "type" = "box",
                        "outline" = 1,
                        "size" = table(
                            "width" = "100%",
                            "height" = "67%"
                        ),
                        "children" = table(
                            table(
                                "name" = "image",
                                "type" = "box"
                            )
                        )
                    ),
                    table(
                        "name" = "label",
                        "type" = "box",
                        "colour" = vec4(150, 150, 150, 255),
                        "position" = table(
                            "x" = 0,
                            "y" = "67%"
                        ),
                        "size" = table(
                            "width" = "100%",
                            "height" = "33%"
                        ),
                        "material" = "gui/gradient_up",
                        "children" = table(
                            table(
                                "name" = "text",
                                "type" = "text",
                                "colour" = vec4(255, 255, 255, 255),
                                "text" = "",
                                "align" = vec2(1, 1),
                                "font" = table(
                                    "font" = "",
                                    "size" = 20
                                )
                            )
                        )
                    )
                )
            )
        )
    }
    
    foreach(K:number, ItemName:string = array("P228", "Glock", "USP", "Five Seven", "Berettes", "Scout")) {
        local Item = ITEMS[ItemName, table]
        COLUMN01:dChildren():pushTable(
            table(
                "type" = "text",
                "colour" = vec4(Item["colour", vector], 255),
                "text" = ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma(),
                "align" = vec2(0, 1),
                "position" = table(
                    "x" = 0,
                    "y" = (K - 1) * 28
                ),
                "size" = table(
                    "width" = "100%",
                    "height" = "15%"
                )
            )
        )
    }
    
    foreach(K:number, ItemName:string = array("XM1014", "Mac10", "MP5", "Famas", "P90", "AUG")) {
        local Item = ITEMS[ItemName, table]
        COLUMN02:dChildren():pushTable(
            table(
                "type" = "text",
                "colour" = vec4(Item["colour", vector], 255),
                "text" = ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma(),
                "align" = vec2(1, 1),
                "position" = table(
                    "x" = 0,
                    "y" = (K - 1) * 28
                ),
                "size" = table(
                    "width" = "100%",
                    "height" = "15%"
                )
            )
        )
    }
    
    foreach(K:number, ItemName:string = array("Deagle", "M4A1S", "AK47", "AWP", "Knife")) {
        local Item = ITEMS[ItemName, table]
        COLUMN03:dChildren():pushTable(
            table(
                "type" = "text",
                "colour" = vec4(Item["colour", vector], 255),
                "text" = ItemName + " - $" + (BET * Item["multiplier", number] * MULTIPLIER):comma(),
                "align" = vec2(1, 1),
                "position" = table(
                    "x" = 0,
                    "y" = (K - 1) * 28
                ),
                "size" = table(
                    "width" = "100%",
                    "height" = "15%"
                )
            )
        )
    }
}

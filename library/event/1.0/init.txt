@persist EVENT_ID:number EVENT_QUEUE:table HASH:string

#include "library/number"
#include "library/string"
#include "library/array"

if(first() || dupefinished()) {
    EVENT_ID = 0
    EVENT_QUEUE = table()
    HASH = hashNoComments():toString()
    
    function number eEventClk() {
        return dsClk() && dsClkName():startsWith(HASH + ":event:")
    }
    
    function number eEventClk(Event:string) {
        return dsClk(HASH + ":event:" + Event)
    }
    
    function string eEventClkName() {
        return dsClkName():sub((HASH + ":event:"):length() + 1, dsClkName():length())
    }
    
    function void eEventPush(Event:string, Time:number, Payload:table) {
        if(Time > 0) {
            local Timer = table(
                "event" = Event,
                "payload" = Payload
            )
            
            local TimerId = HASH + ":event:timer-" + EVENT_ID
            EVENT_ID++
            EVENT_QUEUE[TimerId, table] = Timer
            timer(TimerId, Time)
        } else {
            dsSendDirect(HASH + ":event:" + Event, entity(), Payload)
        }
    }
    
    function void eEventPush(Event:string, Time:number) {
        if(Time > 0) {
            local Timer = table(
                "event" = Event
            )
            
            local TimerId = HASH + ":event:timer-" + EVENT_ID
            EVENT_ID++
            EVENT_QUEUE[TimerId, table] = Timer
            timer(TimerId, Time)
        } else {
            dsSendDirect(HASH + ":event:" + Event, entity(), 0)
        }
    }
    
    function void eEventPush(Event:string, Payload:table) {
        eEventPush(Event, 0, Payload)
    }
    
    function void eEventPush(Event:string) {
        eEventPush(Event, 0)
    }
    
    function table eEventGetPayload() {
        if(!eEventClk()) {
            return table()
        }
        
        return dsGetTable()
    } 
}

if(clkName():startsWith(HASH + ":event:")) {
    local TimerId = clkName()
    if(EVENT_QUEUE:exists(TimerId)) {
        local Event = EVENT_QUEUE[TimerId, table]
        EVENT_QUEUE:remove(TimerId)
        
        if(Event:exists("payload")) {
            eEventPush(Event["event", string], 0, Event["payload", table])
        } else {
            eEventPush(Event["event", string], 0)
        }
    }
}
